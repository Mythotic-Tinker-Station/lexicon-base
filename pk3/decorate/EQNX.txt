Actor Lexicon_EQNX_Pistol : DoomWeapon
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Weapon.SlotNumber 2
	Obituary "$OB_MPPISTOL"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	+WEAPON.CHEATNOTWEAPON
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Inventory.PickupSound "EQNX/DSWPNUP"
	Tag "$TAG_PISTOL"
	Radius 20
	Height 16
	States
	{
	Ready:
		LX56 E 1 A_WeaponReady
		Loop
	Deselect:
		LX56 E 1 A_Lower
		Loop
	Select:
		LX56 E 1 A_Raise
		Loop
	Fire:
		LX56 E 4
		LX56 F 0 A_FireBullets(5.6,0,1,5)
		LX56 F 0 A_PlaySound("EQNX/DSPISTOL",CHAN_WEAPON)
		LX56 F 6 A_GunFlash
		LX56 G 4
		LX56 F 5 A_ReFire
		Goto Ready
	Flash:
		LX56 J 7 Bright A_Light1
		Goto LightDone
		LX56 J 7 Bright A_Light1
		Goto LightDone
 	Spawn:
		PIST A -1
		Stop
	}
}

Actor Lexicon_EQNX_Shotgun : DoomWeapon
{
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Weapon.SlotNumber 3
	Weapon.SlotPriority 0
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTSHOTGUN"
	Inventory.PickupSound "EQNX/DSWPNUP"
	Obituary "$OB_MPSHOTGUN"
	Tag "$TAG_SHOTGUN"
	States
	{
	Ready:
		SHTG A 1 A_WeaponReady
		Loop
	Deselect:
		SHTG A 1 A_Lower
		Loop
	Select:
		SHTG A 1 A_Raise
		Loop
	Fire:
		SHTG A 3
		SHTG A 0 A_FireBullets(5.6,0,7,5)
		SHTG A 0 A_PlaySound("EQNX/DSSHOTGN",CHAN_WEAPON)
		SHTG A 7 A_GunFlash
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	Flash:
		SHTF A 4 Bright A_Light1
		SHTF B 3 Bright A_Light2
		Goto LightDone
	Spawn:
		SHOT A -1
		Stop
	}
}

Actor Lexicon_EQNX_Chaingun : DoomWeapon
{
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Weapon.SlotNumber 4
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTCHAINGUN"
	Inventory.PickupSound "EQNX/DSWPNUP"
	Obituary "$OB_MPCHAINGUN"
	Tag "$EQNX_TAG_CHAINGUN"
	States
	{
	Ready:
		LX56 A 1 A_WeaponReady
		Loop
	Deselect:
		LX56 A 1 A_Lower
		Loop
	Select:
		LX56 A 1 A_Raise
		Loop
	Fire:
		LX56 A 0 A_FireBullets(5.6,0,1,5)
		LX56 A 0 A_PlaySound("EQNX/DSPISTOL",CHAN_WEAPON)
		LX56 A 4 A_GunFlash
		LX56 B 0 A_FireBullets(5.6,0,1,5)
		LX56 B 0 A_PlaySound("EQNX/DSPISTOL",CHAN_WEAPON)
		LX56 B 4 A_GunFlash("Flash2")
		LX56 B 0 A_ReFire
		Goto Ready
	Flash:
		LX56 C 5 Bright A_Light1
		Goto LightDone
	Flash2:
		LX56 D 5 Bright A_Light2
		Goto LightDone
	Spawn:
		LX56 K -1
		Stop
	}
}

Actor Lexicon_EQNX_RocketLauncher : DoomWeapon
{
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Weapon.SlotNumber 5
	+WEAPON.NOAUTOFIRE
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTLAUNCHER"
	Inventory.PickupSound "EQNX/DSWPNUP"
	Tag "$TAG_ROCKETLAUNCHER"
	States
	{
	Ready:
		MISG A 1 A_WeaponReady
		Loop
	Deselect:
		MISG A 1 A_Lower
		Loop
	Select:
		MISG A 1 A_Raise
		Loop
	Fire:
		MISG B 8 A_GunFlash
		MISG B 12 A_FireCustomMissile("Lexicon_EQNX_Rocket")
		MISG B 0 A_ReFire
		Goto Ready
	Flash:
		MISF A 3 Bright A_Light1
		MISF B 4 Bright
		MISF CD 4 Bright A_Light2
		Goto LightDone
	Spawn:
		LAUN A -1
		Stop
	}
}

Actor Lexicon_EQNX_Rocket : Rocket
{
	SeeSound "EQNX/DSRLAUNC"
	DeathSound "EQNX/DSBAREXP"
	States
	{
	Spawn:
		MISL A 1 Bright
		Loop
	}
}

Actor Lexicon_EQNX_PlasmaRifle : DoomWeapon
{
	Weapon.SelectionOrder 100
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Weapon.SlotNumber 6
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTPLASMA"
	Inventory.PickupSound "EQNX/DSWPNUP"
	Tag "$TAG_PLASMARIFLE"
	States
	{
	Ready:
		PLSG A 1 A_WeaponReady
		Loop
	Deselect:
		PLSG A 1 A_Lower
		Loop
	Select:
		PLSG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		PLSG A 3 A_FireCustomMissile("Lexicon_EQNX_PlasmaBall")
		PLSG B 20 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		PLSF A 4 Bright A_Jump(128,1)
		Goto LightDone
		PLSF B 4 Bright
		Goto LightDone
	Spawn:
		PLAS A -1
		Stop
	}
}

Actor Lexicon_EQNX_PlasmaBall : PlasmaBall { SeeSound "EQNX/DSPLASMA" }

Actor Lexicon_EQNX_TechLamp : TechLamp
{
	States
	{
	Spawn:
		LX57 EFGH 4 BRIGHT
		Loop
	}
}

Actor Lexicon_EQNX_TallGreenColumn : TallGreenColumn
{
	States
	{
	Spawn:
		LX57 P -1
		Stop
	}
}

Actor Lexicon_EQNX_ShortGreenColumn : ShortGreenColumn
{
	States
	{
	Spawn:
		LX56 R -1
		Stop
	}
}

Actor Lexicon_EQNX_HeartColumn : HeartColumn
{
	States
	{
	Spawn:
		LX56 ST 14
		Loop
	}
}

Actor Lexicon_EQNX_EvilEye : EvilEye
{
	States
	{
	Spawn:
		LX57 IJKJ 6 BRIGHT
		Loop
	}
}

Actor Lexicon_EQNX_BlueTorch : BlueTorch
{
	States
	{
	Spawn:
		LX57 ABCD 4 BRIGHT
		Loop
	}
}

Actor Lexicon_EQNX_GreenTorch : GreenTorch
{
	States
	{
	Spawn:
		LX56 UVWX 4 BRIGHT
		Loop
	}
}

Actor Lexicon_EQNX_RedTorch : RedTorch
{
	States
	{
	Spawn:
		LX56 LMNO 4 BRIGHT
		Loop
	}
}

Actor Lexicon_EQNX_ShortGreenTorch : ShortGreenTorch
{
	States
	{
	Spawn:
		LX57 LMNO 4 BRIGHT
		Loop
	}
}

Actor Lexicon_EQNX_ExplosiveBarrel : ExplosiveBarrel
{
	DeathSound "EQNX/DSBAREXP"
}

Actor Lexicon_EQNX_RedCard : CustomInventory
{
	Inventory.PickupMessage "$GOTREDCARD"
	States
	{
	Spawn:
		LX57 Q 10
		LX57 R 10 bright
		Loop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("SharedKey_Pickup", 0, 0)
		Stop
	}
}

Actor Lexicon_EQNX_BossBrain : BossBrain
{
	PainSound "EQNX/DSBOSPN"
	DeathSound "EQNX/DSBOSDTH"
	States
	{
	Pain:
		BBRN B 36 A_PlaySound("EQNX/DSBOSPN", CHAN_VOICE, 1.0, 0, ATTN_NONE)
		Goto Spawn
	Death:
		BBRN A 0 A_BrainScream
		BBRN A 100 A_PlaySound("EQNX/DSBOSDTH", CHAN_VOICE, 1.0, 0, ATTN_NONE)
		BBRN AA 10
		BBRN A -1 A_BrainDie
		Stop
	}
}

Actor Lexicon_EQNX_BossEye : BossEye
{
	States
	{
	See:
		SSWV A 181 A_PlaySound("EQNX/DSBOSSIT", CHAN_VOICE, 1.0, 0, ATTN_NONE)
		SSWV A 150 A_BrainSpit("Lexicon_EQNX_SpawnShot")
		Wait
	}
}

Actor Lexicon_EQNX_SpawnShot : SpawnShot { SeeSound "EQNX/DSBOSPIT" }

Actor Lexicon_EQNX_Fatso : Fatso
{
	States
	{
	Missile:
		FATT G 20 A_FatRaise
		FATT H 10 BRIGHT A_FatAttack1("Lexicon_EQNX_FatShot")
		FATT IG 5
		FATT H 10 BRIGHT A_FatAttack2("Lexicon_EQNX_FatShot")
		FATT IG 5
		FATT H 10 BRIGHT A_FatAttack3("Lexicon_EQNX_FatShot")
		FATT IG 5
		Goto See
	}
}

Actor Lexicon_EQNX_FatShot : FatShot { SeeSound "EQNX/DSFIRSHT" }

Actor Lexicon_EQNX_Arachnotron : Arachnotron
{
	States
	{
	Missile:
		BSPI A 20 Bright A_FaceTarget
		TNT1 A 0 A_FaceTarget
		BSPI G 4 Bright A_CustomMissile("Lexicon_EQNX_ArachnotronPlasma")
		BSPI H 4 Bright
		BSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	}
}

Actor Lexicon_EQNX_ArachnotronPlasma : ArachnotronPlasma { SeeSound "EQNX/DSPLASMA" }

Actor Lexicon_EQNX_HellKnight : HellKnight
{
	States
	{
	Melee:
	Missile:
		BOS2 EF 8 A_FaceTarget
		BOS2 G 8 A_CustomComboAttack("Lexicon_EQNX_BaronBall", 32, 10 * random(1, 8), "baron/melee")
		Goto See
	}
}

Actor Lexicon_EQNX_BaronOfHell : BaronOfHell
{
	States
	{
	Melee:
	Missile:
		BOSS EF 8 A_FaceTarget
		BOSS G 8 A_CustomComboAttack("Lexicon_EQNX_BaronBall", 32, 10 * random(1, 8), "baron/melee")
		Goto See
	}
}

Actor Lexicon_EQNX_BaronBall : BaronBall { SeeSound "EQNX/DSFIRSHT" }

Actor Lexicon_EQNX_DoomImp : DoomImp
{
	States
	{
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_CustomComboAttack("Lexicon_EQNX_DoomImpBall", 32, 3 * random(1, 8), "imp/melee")
		Goto See
	}
}

Actor Lexicon_EQNX_DoomImpBall : DoomImpBall { SeeSound "EQNX/DSFIRSHT" }

Actor Lexicon_EQNX_Cacodemon : Cacodemon
{
	States
	{
	Missile:
		HEAD B 5 A_FaceTarget
		HEAD C 5 A_FaceTarget
		HEAD D 5 BRIGHT A_CustomComboAttack("Lexicon_EQNX_CacodemonBall", 32, 10 * random(1, 6))
		Goto See
	}
}

Actor Lexicon_EQNX_CacodemonBall : CacodemonBall { SeeSound "EQNX/DSFIRSHT" }

Actor Lexicon_EQNX_Cyberdemon : Cyberdemon
{
	States
	{
	Missile:
		CYBR E 6 A_FaceTarget
		CYBR F 12 A_CustomMissile("Lexicon_EQNX_Rocket")
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CustomMissile("Lexicon_EQNX_Rocket")
		CYBR E 12 A_FaceTarget
		CYBR F 12 A_CustomMissile("Lexicon_EQNX_Rocket")
		Goto See
	}
}

Actor Lexicon_EQNX_Zombieman : Zombieman  
{
	AttackSound "EQNX/DSPISTOL"
	States
	{
	Missile:
		POSS E 10 A_FaceTarget
		POSS F 8 A_CustomBulletAttack(22.5, 0, 1, random(1,5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
		POSS E 8
		goto See
	}
}

Actor Lexicon_EQNX_ShotgunGuy : ShotgunGuy  { AttackSound "EQNX/DSSHOTGN" DropItem "Lexicon_EQNX_Shotgun" }
Actor Lexicon_EQNX_ChaingunGuy : ChaingunGuy  { AttackSound "EQNX/DSSHOTGN" DropItem "Lexicon_EQNX_Chaingun" }